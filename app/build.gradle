apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion = 27
    buildToolsVersion = '28.0.3'

    defaultConfig {
        applicationId = "com.android.keepass"
        minSdkVersion 14
        targetSdkVersion 26

        versionCode = 178
        versionName = "2.3.4"

        testApplicationId = "com.keepassdroid.tests"
        testInstrumentationRunner = "android.test.InstrumentationTestRunner"

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }


    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "store"

    productFlavors {
        general {
            dimension = "store"
        }

        play {
            dimension = "store"
        }
    }


}

dependencies {
    androidTestImplementation files('libs/junit4.jar')

    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation 'joda-time:joda-time:2.9.4'
    implementation 'com.android.support:support-compat:27.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.14"
    }
}

apply plugin: "net.ltgt.errorprone"

tasks.withType(JavaCompile) {
    options.compilerArgs += [
        '-Xep:ClassCanBeStatic:OFF',
        '-Xep:DefaultCharset:OFF',
        '-Xep:EqualsHashCode:OFF',
        '-Xep:FragmentInjection:OFF',
        '-Xep:HidingField:OFF',
        '-Xep:JdkObsolete:OFF',
        '-Xep:MissingOverride:OFF',
        '-Xep:NonOverridingEquals:OFF',
        '-Xep:ShortCircuitBoolean:OFF',
    ]
}

configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.3.1'
}
